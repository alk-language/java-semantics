
swap(out a, i, j) {
  temp = a[i];
  a[i] = a[j];
  a[j] = temp;
}

/*
@input: a[p..q]
@output: k si a[p..q] partitionat de k
*/
partitioneaza(out a, p, q) {
  m = p + random(q-p);
  swap(a, p, m);
  x = a[p] ; 
  i = p + 1;   j = q;
  while (i <= j) {
    if (a[i] <= x) i = i+1;
    else if (a[j] >= x) j = j-1;
    else if (a[i] > x && x > a[j]) {
      swap(a, i, j);
      i = i+1;
      j = j-1;
    }
  }
  k = i-1;  a[p] = a[k];  a[k] = x;
//  if (k == q) --k;
  return k;
}

/*
@input: a[p..q]
@output: a[p..q] sortat 
*/
nqsort(out a, p, q) {
  if (p < q) {
    k = partitioneaza(a, p, q);
    nqsort(a, p, k-1);
    nqsort(a, k+1, q);
  }
}

nqsort(b, 0, n-1);


/*
alki nqsort.alk "n |-> 5 b |-> [3,1,5,2,7]"
*/




